import QtQuick
import Quickshell
import Quickshell.Hyprland
import Quickshell.Io
import qs.components

Pill {
    id: root

    property string submap: ""
    readonly property bool hyprlandActive:
        (Quickshell.env("XDG_CURRENT_DESKTOP") ?? "").toLowerCase().split(":").includes("hyprland")

    visible: hyprlandActive && submap.length > 0
    text: " " + submap
    tooltipText: "Modo: " + root.submap
    textColor: theme.green

    function normalizeSubmap(value) {
        const s = String(value ?? "").trim()

        if (s.length === 0
            || s === "default"
            || s === "reset"
            || s === "null"
            || s === "none")
            return ""

        return s
    }

    Process {
        id: initialSubmap
        running: root.hyprlandActive
        command: ["hyprctl", "submap"]

        stdout: StdioCollector {
            onStreamFinished: {
                root.submap = root.normalizeSubmap(this.text)
            }
        }
    }

    Connections {
        target: Hyprland
        enabled: root.hyprlandActive

        function onRawEvent(event) {
            if (event.name === "submap")
                root.submap = root.normalizeSubmap(event.data)
        }
    }
}
